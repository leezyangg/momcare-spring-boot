package rules

import com.upm.momcarerecommendation.domain.model.MotherRequest
import com.upm.momcarerecommendation.domain.model.DetectedDisease

// Detection of Blood Pressure
rule "Mild Blood Pressure"
salience 1
    when
        $mother : MotherRequest( bloodPressure.systolic >= 140, bloodPressure.diastolic >= 90 )
        not DetectedDisease( diseaseName == "Blood Pressure" , mother == $mother)
    then
        System.out.println("You are detected Mild Blood Pressure");
        insert(new DetectedDisease( "Blood Pressure" , "Mild", $mother));
end


rule "Moderate Blood Pressure"
salience 5
    when
        $mother : MotherRequest( bloodPressure.systolic >= 150, bloodPressure.diastolic >= 100)
        not DetectedDisease( diseaseName == "Blood Pressure" , mother == $mother)
    then
        System.out.println("Detected Moderate Blood Pressure");
        insert(new DetectedDisease( "Blood Pressure" , "Moderate", $mother));
end

rule "Severe Blood Pressure"
salience 10
    when
        $mother : MotherRequest( bloodPressure.systolic >= 160, bloodPressure.diastolic >= 110)
        not DetectedDisease( diseaseName == "Blood Pressure" , mother == $mother)
    then
        System.out.println("Detected Severe Blood Pressure");
        insert(new DetectedDisease( "Blood Pressure" , "Severe", $mother));
end



// Detection of Blood Sugar Level (based on Fasting Blood Sugar Test) (mmol/L)
rule "Gestational Diabetes"
salience 1
    when
        $mother : MotherRequest( isPregnant(), !historicalDisease.contains("Diabetes"), bloodSugar >= 5.1 )
        not DetectedDisease( diseaseName == "Diabetes" , mother == $mother)
    then
        System.out.println("Detected Gestational Diabetes");
        insert(new DetectedDisease( "Diabetes", "Gestational", $mother));
end

rule "Having Diabetes Before Pregnant or postpartum Diabetes"
    when
        $mother : MotherRequest ( bloodSugar >= 5.1 )
        not DetectedDisease( diseaseName == "Diabetes", mother == $mother)
    then
        System.out.println("Detected Diabetes");
        insert(new DetectedDisease( "Diabetes", $mother));
end



// Detection of Anaemia (g/dL)
rule "Iron Deficiency Anaemia for Trimester 1"
    when
        $mother : MotherRequest( isPregnant(), trimester == 1, haemoglobinLevel <= 11 )
    then
        System.out.println("Detected Anaemia");
        insert(new DetectedDisease( "Anaemia", $mother));
end

rule "Iron Deficiency Anaemia for Trimester 2"
    when
        $mother : MotherRequest( isPregnant(), trimester == 2, haemoglobinLevel <= 10.5 )
    then
        System.out.println("Detected Anaemia");
        insert(new DetectedDisease( "Anaemia", $mother));
end

rule "Iron Deficiency Anaemia for Trimester 3"
    when
        $mother : MotherRequest( isPregnant(), trimester == 3, haemoglobinLevel <= 10 )
    then
        System.out.println("Detected Anaemia");
        insert(new DetectedDisease( "Anaemia", $mother));
end
